[doc test apply-template action]

[shell setup]
    !ncs-netsim reset
    ?SH-PROMPT
    !echo ==$?==
    ?==0==
    ?SH-PROMPT

    !ncs-setup --reset
    ?SH-PROMPT
    !echo ==$?==
    ?==0==
    ?SH-PROMPT

    !ncs-netsim start
    ?SH-PROMPT
    !echo ==$?==
    ?==0==
    ?SH-PROMPT

    !(cd ncs-runtime && ncs)
    ?SH-PROMPT
    !echo ==$?==
    ?==0==
    ?SH-PROMPT


[global HEADERS=-H "Accept: application/vnd.yang.data+json" -H "Content-Type: application/vnd.yang.data+json"]
[global AUTH=-u admin:admin]
[global URL=http://localhost:8080/api/running]


[shell rest]
    -[Ee]rror

    !curl ${AUTH} ${HEADERS} -X POST ${URL}/devices/_operations/sync-from
    ??"result": true
    ??"result": true
    ?SH-PROMPT

    !curl ${AUTH} ${HEADERS} -X POST ${URL}/template/_operations/apply-config-template -T - <<EOF
    !{ "apply-config-template": {
    !    "name": "frobnicator-foo",
    !    "variable": { "name": "DEVICE", "value": "frob0" }
    !  }
    !}
    !EOF
#    ??"result": "ok"
    ?SH-PROMPT

    !curl ${AUTH} ${HEADERS} -X GET ${URL}/devices/device/frob0/config/frobnicator/system-name
    ?SH-PROMPT

    !curl ${AUTH} ${HEADERS} -X GET ${URL}/devices/device/frob0/config/frobnicator/a
    ?SH-PROMPT

    !curl ${AUTH} ${HEADERS} -X GET ${URL}/devices/device/frob0/config/frobnicator/b
    ?SH-PROMPT


    # Non-existent template, expect errors
    -
    !curl ${AUTH} ${HEADERS} -X POST ${URL}/template/_operations/apply-config-template -T - <<EOF
    !{ "apply-config-template": {
    !    "name": "xyz",
    !    "variable": { "name": "DEVICE", "value": "frob0" }
    !  }
    !}
    !EOF
    ?errors
    ?The template file xyz has not been loaded
    ?SH-PROMPT




[cleanup]
    !ncs-netsim stop
    ?SH-PROMPT
    !echo ==$?==
    ?==[0-9][0-9]*==
    ?SH-PROMPT

    !ncs --stop
    ?SH-PROMPT
    !echo ==$?==
    ?==[0-9][0-9]*==
    ?SH-PROMPT

